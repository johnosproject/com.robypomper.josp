import com.robypomper.build.java.JavaRunnableUtils

sourceSets {
    jospCore {
        java {
            srcDirs 'src/jospCore/java_public'   // interfaces only
            srcDirs 'src/jospCore/java'          // main jcp classes
            srcDirs 'src/jospCore/java_impl002'  // implementations for v 0.0.2
        }
    }
    jospJOD {
        java {
            srcDirs 'src/jospJOD/java_public'   // interfaces only
            srcDirs 'src/jospJOD/java'          // main jod classes
            srcDirs 'src/jospJOD/java_mains'    // main runnable classes
            srcDirs 'src/jospJOD/java_impl002'  // implementations for v 0.0.2
        }
    }
    jospJSL {
        java {
            srcDirs 'src/jospJSL/java_public'   // interfaces only
            srcDirs 'src/jospJSL/java'          // main jod classes
            srcDirs 'src/jospJSL/java_mains'    // main runnable classes
            srcDirs 'src/jospJSL/java_impl002'  // implementations for v 0.0.2
        }
    }
}

dependencies {
    // Core - All
    jospCoreImplementation sourceSets.jospCommons.output
    jospCoreImplementation sourceSets.jospCommons.runtimeClasspath

    // JOD - All
    jospJODImplementation sourceSets.jospCore.output
    jospJODImplementation configurations.jospCoreImplementation
    jospJODImplementation sourceSets.jcpApisPublic.output
    jospJODImplementation sourceSets.jcpApisPublic.runtimeClasspath
    // JOD - Mains
    jospJODImplementation 'commons-cli:commons-cli:1.4'
    jospJODImplementation 'com.googlecode.clichemaven:cliche:110413'

    // JSL - All
    jospJSLImplementation sourceSets.jospCore.output
    jospJSLImplementation configurations.jospCoreImplementation
    // JSL - Impls
    jospJSLImplementation sourceSets.jcpApisPublic.output
    jospJSLImplementation sourceSets.jcpApisPublic.runtimeClasspath
    // JSL - Mains
    jospJSLImplementation 'commons-cli:commons-cli:1.4'
    jospJSLImplementation 'com.googlecode.clichemaven:cliche:110413'
}


// Make Java Runnable

def makeJODJavaRunnable(String runnableName, String config_yml, String struct_jod, String perms_jod) {
    runnableName = "JOD${runnableName}"
    String mainClass = 'com.robypomper.josp.jod.JODShell'
    File workingDir = file("envs/runnables/jod/${runnableName}")

    JavaRunnableUtils.makeJavaFromSourceSet(project, sourceSets.jospJOD, mainClass, runnableName, workingDir)
    String jarTaskName = "java${runnableName}Run"

    tasks.getByName(jarTaskName) {
        group 'application jod'
        standardInput = System.in
        //args = ['--configs', 'src/jospJOD/configs/jod_dev.yml']
        //jvmArgs = ['-Djavax.net.debug=ssl']

        doFirst {
            delete workingDir
            copy {
                from "src/jospJOD/configs/${config_yml}"
                from "src/jospJOD/configs/${struct_jod}"
                from "src/jospJOD/configs/${perms_jod}"
                rename 'jod(.+).yml', 'jod.yml'
                rename 'struct(.+).jod', 'struct.jod'
                rename 'perms(.+).jod', 'perms.jod'
                into workingDir
            }
        }
    }
}

def makeJSLJavaRunnable(String runnableName, String config_yml) {
    runnableName = "JSL${runnableName}"
    String mainClass = 'com.robypomper.josp.jsl.JSLShell'
    File workingDir = file("envs/runnables/jsl/${runnableName}")

    JavaRunnableUtils.makeJavaFromSourceSet(project, sourceSets.jospJSL, mainClass, runnableName, workingDir)
    String jarTaskName = "java${runnableName}Run"

    tasks.getByName(jarTaskName) {
        group 'application jsl'
        standardInput = System.in
        //args = ['--configs', 'src/jospJSL/configs/jsl_dev.yml']
        //jvmArgs = ['-Djavax.net.debug=ssl']

        doFirst {
            delete workingDir
            copy {
                from "src/jospJSL/configs/${config_yml}"
                rename 'jsl(.+).yml', 'jsl.yml'
                into workingDir
            }
        }
    }
}



// JOD
/*
 * ObjId    =>  unset   local   cloud
 * Owner    =>  unset   set
 *
 * Configs (ObjId, OwnerId, Off/Online)
 */

makeJODJavaRunnable('uuf','jod_uuf.yml','struct_default.jod','perms_default.jod')
makeJODJavaRunnable('uun','jod_uun.yml','struct_default.jod','perms_default.jod')
makeJODJavaRunnable('usf','jod_usf.yml','struct_default.jod','perms_default.jod')
makeJODJavaRunnable('usn','jod_usn.yml','struct_default.jod','perms_default.jod')

makeJODJavaRunnable('luf','jod_luf.yml','struct_default.jod','perms_default.jod')
makeJODJavaRunnable('lun','jod_lun.yml','struct_default.jod','perms_default.jod')
makeJODJavaRunnable('lsf','jod_lsf.yml','struct_default.jod','perms_default.jod')
makeJODJavaRunnable('lsn','jod_lsn.yml','struct_default.jod','perms_default.jod')

makeJODJavaRunnable('cuf','jod_cuf.yml','struct_default.jod','perms_default.jod')
makeJODJavaRunnable('cun','jod_cun.yml','struct_default.jod','perms_default.jod')
makeJODJavaRunnable('csf','jod_csf.yml','struct_default.jod','perms_default.jod')
makeJODJavaRunnable('csn','jod_csn.yml','struct_default.jod','perms_default.jod')


// JSL

/**
 * Usr    =>  unset   set   anonymous
 *            when set, check that set token is valid and not expired
 *
 * Configs (Usr, Off/Online)
 */

makeJSLJavaRunnable('uf','jsl_uf.yml')
makeJSLJavaRunnable('un','jsl_un.yml')
makeJSLJavaRunnable('sf','jsl_sf.yml')
makeJSLJavaRunnable('sn','jsl_sn.yml')



// Tests

sourceSets {
    jospJODCommTest {
        java {
            compileClasspath += sourceSets.jospJOD.output
            runtimeClasspath += sourceSets.jospJOD.output
            compileClasspath += sourceSets.jospJSL.output
            runtimeClasspath += sourceSets.jospJSL.output
            srcDirs = ['src/jospJODTest/comm/java']
        }
    }
    jospJSLCommTest {
        compileClasspath += sourceSets.jospJSL.output
        runtimeClasspath += sourceSets.jospJSL.output
        compileClasspath += sourceSets.jospJOD.output
        runtimeClasspath += sourceSets.jospJOD.output
        java.srcDirs = ['src/jospJSLTest/comm/java']
    }
}

configurations {
    jospJODCommTestImplementation.extendsFrom jospJODImplementation
    jospJODCommTestRuntimeOnly.extendsFrom jospJODRuntimeOnly

    jospJSLCommTestImplementation.extendsFrom jospJSLImplementation
    jospJSLCommTestRuntimeOnly.extendsFrom jospJSLRuntimeOnly
}

dependencies {
    jospJODCommTestImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
    jospJODCommTestRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.0'

    jospJSLCommTestImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
    jospJSLCommTestRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
}

task jospJODTest {
    description = 'Runs all JOD tests.'
    group = 'verification'
}

task jospJODCommTest(type: Test) {
    description = 'Runs JOD Communication tests.'
    group = 'verification'

    testClassesDirs = sourceSets.jospJODCommTest.output.classesDirs
    classpath = sourceSets.jospJODCommTest.runtimeClasspath
    shouldRunAfter test
    useJUnitPlatform()
    //jvmArgs '-Djavax.net.debug=all'
}


task jospJSLTest {
    description = 'Runs all JSL tests.'
    group = 'verification'
}

task jospJSLCommTest(type: Test) {
    description = 'Runs JSL Communication tests.'
    group = 'verification'

    testClassesDirs = sourceSets.jospJSLCommTest.output.classesDirs
    classpath = sourceSets.jospJSLCommTest.runtimeClasspath
    shouldRunAfter test
    useJUnitPlatform()
    //jvmArgs '-Djavax.net.debug=all'
}


jospJODTest.dependsOn jospJODCommTest
check.dependsOn jospJODTest

jospJSLTest.dependsOn jospJSLCommTest
check.dependsOn jospJSLTest


// gradle > intellij settings
idea {
    module {
        testSourceDirs += project.sourceSets.jospJODCommTest.java.srcDirs
        testSourceDirs += project.sourceSets.jospJODCommTest.resources.srcDirs
        testSourceDirs += project.sourceSets.jospJSLCommTest.java.srcDirs
        testSourceDirs += project.sourceSets.jospJSLCommTest.resources.srcDirs
    }
}