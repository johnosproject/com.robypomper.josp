import com.robypomper.build.java.JavaRunnableUtils

sourceSets {
    jospCommons {
        java {
            srcDirs 'src/jospCommons/java_public'   // interfaces only
            srcDirs 'src/jospCommons/java'          // main jcp classes
            srcDirs 'src/jospCommons/java_impl002'  // implementations for v 0.0.2
        }
    }
    jospCore {
        java {
            srcDirs 'src/jospCore/java_public'   // interfaces only
            srcDirs 'src/jospCore/java'          // main jcp classes
            srcDirs 'src/jospCore/java_impl002'  // implementations for v 0.0.2
        }
    }
    jospJOD {
        java {
            srcDirs 'src/jospJOD/java_public'   // interfaces only
            srcDirs 'src/jospJOD/java'          // main jod classes
            srcDirs 'src/jospJOD/java_mains'    // main runnable classes
            srcDirs 'src/jospJOD/java_impl002'  // implementations for v 0.0.2
        }
    }
    jospJSL {
        java {
            srcDirs 'src/jospJSL/java_public'   // interfaces only
            srcDirs 'src/jospJSL/java'          // main jod classes
            srcDirs 'src/jospJSL/java_mains'    // main runnable classes
            srcDirs 'src/jospJSL/java_impl002'  // implementations for v 0.0.2
        }
    }
}

dependencies {
    // Commons - Internal
    jospCommonsCompileOnly 'org.projectlombok:lombok:1.18.12'                                   // Lombok annotations
    jospCommonsAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
    jospCommonsImplementation 'org.hibernate:hibernate-core:5.4.10.Final'
    jospCommonsImplementation 'com.fasterxml.jackson.core:jackson-annotations:2.10.2'
    jospCommonsImplementation 'com.github.scribejava:scribejava-core:6.9.0'
    jospCommonsImplementation 'com.github.scribejava:scribejava-apis:6.9.0'


    // Core - Internal
    jospCoreImplementation sourceSets.jospCommons.output
    jospCoreImplementation sourceSets.jospCommons.runtimeClasspath

    // JOD - All
    jospJODImplementation sourceSets.jospCore.output
    jospJODImplementation configurations.jospCoreImplementation
    // JOD - Impls
    jospJODImplementation 'org.yaml:snakeyaml:1.21'
    // JOD - Mains
    jospJODImplementation 'commons-cli:commons-cli:1.4'
    jospJODImplementation 'com.googlecode.clichemaven:cliche:110413'

    // JSL - All
    jospJSLImplementation sourceSets.jospCore.output
    jospJSLImplementation configurations.jospCoreImplementation
}

// Add java app's run tasks
JavaRunnableUtils.makeJavaFromSourceSet(project,sourceSets.jospJOD,'com.robypomper.josp.jod.JODShell')
JavaRunnableUtils.makeJavaFromSourceSet(project,sourceSets.jospJSL,'com.robypomper.josp.jsl.JODShell')

tasks.javaJospJODRun {
    standardInput = System.in
    args = ['--configs', 'src/jospJOD/configs/jod_dev.yml']
}