import com.robypomper.build.java.JavaPublicationUtils

/* *****************************************************************************
 * The John Operating System Project is the collection of software and configurations
 * to generate IoT EcoSystem, like the John Operating System Platform one.
 * Copyright (C) 2020 Roberto Pompermaier
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 **************************************************************************** */

/**
 * Gradle configurations to manage JOSP Commons as java implementations.
 *
 * Components list:
 * <ul>
 *     <li>
 *         JOSP Commons
 *     </li>
 * </ul>
 *
 * Summary:
 * <ul>
 *     <li>
 *         Components configs
 *     </li>
 *     <li>
 *         Runners Tasks: no runners
 *     </li>
 *     <li>
 *         Publication configs
 *     </li>
 *     <li>
 *         Publication tasks
 *     </li>
 * </ul>
 */


// ------------------
// Components configs
// ------------------

sourceSets {

    // JOSP APIs
    jospAPIs {}

    // JOSP Commons
    jospCommons {}

}

configurations {}

dependencies {

    // JOSP APIs
    jospAPIsCompileOnly "org.projectlombok:lombok:${project.ext.get("version.lombok")}"                                 // Lombok annotations
    jospAPIsAnnotationProcessor "org.projectlombok:lombok:${project.ext.get("version.lombok")}"
    jospAPIsImplementation "com.fasterxml.jackson.core:jackson-databind:${project.ext.get("version.jackson")}"
    jospAPIsImplementation "com.fasterxml.jackson.core:jackson-annotations:${project.ext.get("version.jackson")}"

    // JOSP Commons
    if (!project.ext.get("deps.jospAPIsDisabled"))
        jospCommonsImplementation "com.robypomper.josp:jospAPIs:${project.ext.get("version.jospAPIs")}"
    jospCommonsImplementation "org.apache.logging.log4j:log4j-api:${project.ext.get("version.apache.log4j")}"           // Logger
    jospCommonsImplementation "org.apache.logging.log4j:log4j-api:${project.ext.get("version.apache.log4j")}"           // Logger
    jospCommonsImplementation "org.apache.logging.log4j:log4j-core:${project.ext.get("version.apache.log4j")}"
    jospCommonsImplementation "org.yaml:snakeyaml:${project.ext.get("version.snakeyaml")}"                              // YAML
    jospCommonsImplementation "com.github.scribejava:scribejava-core:${project.ext.get("version.scribejava")}"          // HTTP & OAuth2
    jospCommonsImplementation "com.github.scribejava:scribejava-apis:${project.ext.get("version.scribejava")}"
    jospCommonsCompileOnly "org.projectlombok:lombok:${project.ext.get("version.lombok")}"                              // Lombok annotations
    jospCommonsAnnotationProcessor "org.projectlombok:lombok:${project.ext.get("version.lombok")}"
    jospCommonsImplementation "org.jmdns:jmdns:${project.ext.get("version.jmdns")}"                                     // JmDNS

}


// -------------------
// Publication configs
// -------------------

// JOSP Commons
String jospAPIsName = 'JOSP APIs Library'
String jospAPIsArtifact = 'jospAPIs'
String jospAPIsVersion = project.ext.get("version.jospAPIs_DEV")
def jospAPIsPubDesc = new JavaPublicationUtils.Descriptor(project,sourceSets.jospAPIs,jospAPIsName,jospAPIsArtifact,jospAPIsVersion)
        .setUrlsFromProject(project)
        .setLicenceGPLv3()
        .setArtifactOpens()
JavaPublicationUtils.generatePublication(project,jospAPIsPubDesc)

// JOSP Commons
String jospCommonsName = 'JOSP Commons Library'
String jospCommonsArtifact = 'jospCommons'
String jospCommonsVersion = project.ext.get("version.jospCommons_DEV")
def jospCommonsPubDesc = new JavaPublicationUtils.Descriptor(project,sourceSets.jospCommons,jospCommonsName,jospCommonsArtifact,jospCommonsVersion)
        .setUrlsFromProject(project)
        .setLicenceGPLv3()
        .setArtifactOpens()
JavaPublicationUtils.generatePublication(project,jospCommonsPubDesc)


// -----------------
// Publication tasks
// -----------------

task all_PublishToLocal {
    description = 'Publish...'
    group = 'JOSP publishers (locals)'
}

task josp_PublishToLocal {
    description = 'Publish...'
    group = 'JOSP publishers (locals)'
    tasks.all_PublishToLocal.dependsOn josp_PublishToLocal
}

// JOSP APIs
task jospAPIs_PublishToLocal {
    description = 'Publish...'
    group = 'JOSP publishers (locals)'
    dependsOn publishJospAPIsPublicationToMavenLocal
    tasks.josp_PublishToLocal.dependsOn jospAPIs_PublishToLocal
}

// JOSP Commons
task jospCommons_PublishToLocal {
    description = 'Publish...'
    group = 'JOSP publishers (locals)'
    dependsOn publishJospCommonsPublicationToMavenLocal
    tasks.josp_PublishToLocal.dependsOn jospCommons_PublishToLocal
}
