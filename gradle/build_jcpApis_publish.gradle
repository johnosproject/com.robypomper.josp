import com.robypomper.build.java.JavaPublicationUtils

// --------------------------
// Jar Tasks for publications
// --------------------------

// JOSP Commons (Docs and Src)
def jcpAPIsName = 'JOSP Cloud Platform Public Library'
def jcpAPIsArtifact = 'jcpAPIs'
def jcpAPIsVersion = '2.0.0'

task jcpAPIsJar(type: Jar) {
    // JOD source code
    from sourceSets.jcpAPIsPublic.output
    // JOSP Commons and JCP APIs Public
    from {
        sourceSets.jcpAPIsPublic.runtimeClasspath.collect {
            if (!it.exists()) return
            if (!it.isDirectory()) return
            it
        } }

    manifest {
        attributes 'Implementation-Title': jcpAPIsName,
                'Implementation-Version': jcpAPIsVersion
    }
}

task jcpAPIsDocs(type: Javadoc) {
    source sourceSets.jcpAPIsPublic.allJava
    classpath += sourceSets.jcpAPIsPublic.runtimeClasspath
    classpath += sourceSets.jcpAPIsPublic.compileClasspath
    classpath += sourceSets.jcpAPIs.runtimeClasspath
    classpath += sourceSets.jcpAPIs.compileClasspath
}

task jcpAPIsDocsJar(type: Jar) {
    classifier = 'doc'
    from jcpAPIsDocs

    manifest {
        attributes 'Implementation-Title': jcpAPIsName + " - Docs",
                'Implementation-Version': jcpAPIsVersion
    }
}

task jcpAPIsSrcJar(type: Jar) {
    classifier = 'src'

    from sourceSets.jcpAPIsPublic.allSource

    manifest {
        attributes 'Implementation-Title': jcpAPIsName + " - Sources",
                'Implementation-Version': jcpAPIsVersion
    }
}


// ------------
// Publications
// ------------

publishing {
    publications {

        // JCP APIs Public
        jcpAPIs(MavenPublication) {
            groupId = project.group
            artifactId = jcpAPIsArtifact
            version = jcpAPIsVersion

            artifact tasks.jcpAPIsJar
            artifact tasks.jcpAPIsSrcJar
            artifact tasks.jcpAPIsDocsJar

            JavaPublicationUtils.initPom(pom, jcpAPIsName, 'The JCP APIs Public Library', projectUrl, projectGit, projectGitUrl, "AGPLv3", "https://www.gnu.org/licenses/agpl-3.0.html")
            JavaPublicationUtils.injectDependenciesToPom(pom, configurations.jcpAPIsPublicImplementation, true)
        }

    }
}
